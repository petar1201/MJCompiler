package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;

// import java.io.*;
import rs.ac.bg.etf.pp1.ast.*;


parser code {:
	
	boolean errorDetected = false;
	
	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }
    
    public void report_info(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }
    
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}


terminal PROG, LBRACE, RBRACE, SEMI, LPAREN, RPAREN, COMMA, PRINT, RETURN, PLUS, EQUAL, VOID;
terminal DIVIDE, DEC, OR, CONST, CONTINUE, EQ, NEQ, PROGRAM, CLASS, NAMESPACE, RBRACKET, INC;
terminal READ, GT, BREAK, TIMES, MOD, LBRACKET, AND, ARROW, MINUS, ASSIGN;
terminal LTE, DOT, LT, GTE, NEW, EXTENDS, STATIC, FOR, COLON;
terminal Integer NUMCONST;
terminal Boolean BOOLCONST;
terminal char CHARCONST;
terminal String IDENT;
terminal IF, ELSE;

nonterminal ClassOrVarOrConstDeclaration, MultipleMethodDeclaration, MultipleNamespace, Namespace;
nonterminal MethodDecl, ConstDecl, VarDecl, ClassDecl, NumOrCharOrBoolConst;
nonterminal MultipleConstVariablesInOneLineDeclarations, MultipleVariablesInOneLineDeclarations;
nonterminal StaticInitializer, ExtendableClass, StaticVarDeclarations, MultipleStaticInitializers;
nonterminal MultipleVarDeclarations, MultipleMethodDeclarationsForClass, MultipleStatements;
nonterminal Statement, FormPars, TypeIdentOptionalBrackets, MultipleTypeIdentOptionalBrackets;
nonterminal Mulop, Addop, Relop, Assignop, Label;
nonterminal CondFact, CondTerm, Condition, ActPars, DesignatorStatement, MultipleDesignators;
nonterminal MultipleDesignatorStatements;

nonterminal NamespaceIdent, TypeConst, CommaIdent, TypeConstIdent, VarTypeIdent, VarrArrTypeIdent, CommaIdentLbracketRbracket, VoidIdent;

nonterminal rs.etf.pp1.symboltable.concepts.Obj Program, ProgramName, Designator;

nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, Factor, Term, Expr;

Program ::= (Program) PROG ProgramName MultipleNamespace ClassOrVarOrConstDeclaration LBRACE MultipleMethodDeclaration RBRACE;

ProgramName ::= (ProgramName) IDENT:pName;

Namespace ::= (Namespace) NamespaceIdent LBRACE ClassOrVarOrConstDeclaration LBRACE MultipleMethodDeclaration RBRACE RBRACE;

NamespaceIdent ::= (NamespaceIdent) NAMESPACE IDENT:namespaceName;

ConstDecl ::= (ConstDeclaration) TypeConstIdent ASSIGN NumOrCharOrBoolConst MultipleConstVariablesInOneLineDeclarations SEMI;

TypeConstIdent ::= (TypeConstIdent) TypeConst IDENT:constName;

TypeConst ::= (TypeConst) CONST Type:type;

VarDecl ::= (VarDeclarationWithBrackets) VarrArrTypeIdent MultipleVariablesInOneLineDeclarations SEMI
			|(VarDeclarationWithoutBrackets) VarTypeIdent MultipleVariablesInOneLineDeclarations SEMI;

VarTypeIdent ::= (VarTypeIdent) Type:type IDENT:varName;
VarrArrTypeIdent ::= (VarrArrTypeIdent) Type:type IDENT:varName LBRACKET RBRACKET;

ClassDecl ::= (ClassDeclaration) CLASS IDENT ExtendableClass LBRACE StaticVarDeclarations MultipleStaticInitializers MultipleVarDeclarations MultipleMethodDeclarationsForClass RBRACE;

StaticInitializer ::= (StaticInit) STATIC LBRACE MultipleStatements RBRACE;

MethodDecl ::= (VoidMethodDeclaration) VoidIdent LPAREN RPAREN MultipleVarDeclarations LBRACE  MultipleStatements RBRACE;
			
VoidIdent ::= (VoidIdent) VOID IDENT:name;

FormPars ::= (FormParameters) TypeIdentOptionalBrackets MultipleTypeIdentOptionalBrackets;

Type ::= (ExpandedType) IDENT:namespaceName COLON COLON IDENT:typeName | (BasicType) IDENT:typeName;


Statement ::= (StatementDesignatorStatementSemi) DesignatorStatement SEMI
			| (StatementReadParensDesignatorSemi) READ LPAREN Designator RPAREN SEMI
			| (StatementPrintParensExprSemi) PRINT LPAREN Expr RPAREN SEMI
			| (StatementPrintParensExprNumConstSemi) PRINT LPAREN Expr COMMA NUMCONST RPAREN SEMI;
								

DesignatorStatement ::= (DesignatorStatementAssignopExpr) Designator Assignop Expr
					| (DesignatorStatementIncrement) Designator INC 
					| (DesignatorStatementDecrement) Designator DEC;
					

ActPars ::= (MultipleActPats) ActPars COMMA Expr | (BasicActPat) Expr;

Condition ::= (ExpandedCondition) Condition OR CondTerm | (BasicCondition) CondTerm;

CondTerm ::= (ExpandedCondTerm) CondTerm AND CondFact | (BasicCondTerm) CondFact;

CondFact ::= (BasicCondFact) Expr | (ExpandedCondFact) Expr Relop Expr;

Expr ::=  (BasicExprWithMinus) MINUS Term | (MultipleExprTerm) Expr Addop Term | (BasicExpr) Term ;

Term ::= (TermFactorList) Term Mulop Factor | (BasicTermFactor) Factor;

Factor ::= (FactorDesignator) Designator
		| (FactorNum) NUMCONST
		| (FactorChar) CHARCONST
		| (FactorBool) BOOLCONST
		| (FactorNewTypeBracketsExpr) NEW Type LBRACKET Expr RBRACKET
		| (FactorParensExpr) LPAREN Expr RPAREN;

Designator ::= (BasicDesignator) IDENT:designatorName 
			 | (ExtendedDesignator) IDENT:namespaceName COLON COLON IDENT:designatorName
			 | (ArrDesignator) Designator LBRACKET Expr RBRACKET;

Label ::= (Label) IDENT;

Assignop ::= (Assignop) ASSIGN;

Relop ::= (Eq) EQ
		| (Neq) NEQ
		| (Gt) GT
		| (Gte) GTE
		| (Lt) LT
		| (Lte) LTE;
		
Addop ::= (Plus) PLUS
		| (Minus) MINUS;

Mulop ::= (Times) TIMES
		| (Divide) DIVIDE
		| (Mod) MOD;


MultipleDesignatorStatements ::= (MultipleDesignatorStatementsList) MultipleDesignatorStatements COMMA DesignatorStatement
						| (BasicMultipleDesignatorStatements) DesignatorStatement
						| (EmptyMultipleDesignatorStatements);
		
MultipleDesignators ::= (MultipleDesignatorsAddingDesignatorAndComma) MultipleDesignators Designator COMMA
					| (MultipleDesignatorsAddingComma) MultipleDesignators COMMA
					| (EmptyMultipleDesignators);		


TypeIdentOptionalBrackets ::= (TypeIdentOptionalBracketsNoBrackets) Type IDENT | (TypeIdentOptionalBracketsBrackets) Type IDENT LBRACKET RBRACKET;

MultipleTypeIdentOptionalBrackets ::= (MultipleTypeIdentOptionalBracketsList) MultipleTypeIdentOptionalBrackets COMMA TypeIdentOptionalBrackets
								     | (EmptyMultipleTypeIdentOptionalBrackets);

MultipleStatements ::= (MultipleStatementsList) MultipleStatements Statement | (EmptyMultipleStatements);

MultipleMethodDeclarationsForClass ::= (MultipleMethodDeclarationsForClassList) LBRACE MultipleMethodDeclaration RBRACE | (EmptyMultipleMethodDeclarationsForClass);

MultipleVarDeclarations ::= (MultipleVarDeclarationsList) MultipleVarDeclarations VarDecl | (EmptyMultipleVarDeclarations);

MultipleStaticInitializers ::= (StaticInitializerList) StaticInitializer MultipleStaticInitializers | (EmptyStaticInitalizerList);

StaticVarDeclarations ::= (StaticVarDeclarationsList) StaticVarDeclarations STATIC VarDecl | (EmptyStaticVarDeclarations);

ExtendableClass ::= (ClassExtended) EXTENDS Type | (ClassNotExtened);


MultipleVariablesInOneLineDeclarations ::= (MultipleVariablesInOneLineDeclarationsBrackets) MultipleVariablesInOneLineDeclarations CommaIdentLbracketRbracket
										  |(MultipleVariablesInOneLineDeclarationsNoBrackets) MultipleVariablesInOneLineDeclarations CommaIdent
										  | (EmptyMultipleVariablesInOneLineDeclarations);

CommaIdentLbracketRbracket ::= (CommaIdentLbracketRbracket) COMMA IDENT:varName LBRACKET RBRACKET;


NumOrCharOrBoolConst ::= (NumOrCharOrBoolConstCharConst) CHARCONST:charValue
						| (NumOrCharOrBoolConstBoolConst) BOOLCONST:boolValue
						| (NumOrCharOrBoolConstNumConst) NUMCONST:numValue;

MultipleConstVariablesInOneLineDeclarations ::= (MultipleConstVariablesInOneLineDeclarationsList) CommaIdent ASSIGN NumOrCharOrBoolConst MultipleConstVariablesInOneLineDeclarations
												|(EmptyMultipleConstVariablesInOneLineDeclarations);


CommaIdent ::= (CommaIdent)COMMA IDENT:name;

MultipleNamespace ::= (NamespacesOneAndMultiple) MultipleNamespace Namespace | (EmptyNamespace);

ClassOrVarOrConstDeclaration ::= (ListAndConstDecl) ClassOrVarOrConstDeclaration ConstDecl
								| (ListAndVarDecl) ClassOrVarOrConstDeclaration VarDecl
								| (ListAndClassDecl) ClassOrVarOrConstDeclaration ClassDecl
								| (EmptyClassOrVarOrConstDeclaration);

MultipleMethodDeclaration ::= (MethodDeclarationOneAndMultiple) MultipleMethodDeclaration MethodDecl | (EmptyMethodDeclaration);

   

